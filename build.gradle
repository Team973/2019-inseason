plugins {
    id "cpp"
    id "google-test-test-suite"
    id "edu.wpi.first.GradleRIO" version "2019.4.1"
    id "org.ysb33r.doxygen" version "0.5"
}


// Define my targets (RoboRIO) and artifacts (deployable files)
// This is added by GradleRIO's backing project EmbeddedTools.
deploy {
    targets {
        roboRIO("roborio") {
            // Team number is loaded either from the .wpilib/wpilib_preferences.json
            // or from command line. If not found an exception will be thrown.
            // You can use getTeamOrDefault(team) instead of getTeamNumber if you
            // want to store a team number in this file.
            team = frc.getTeamNumber()
        }
    }
    artifacts {
        frcNativeArtifact('frcCpp') {
            targets << "roborio"
            component = 'frcUserProgram'
            // Debug can be overridden by command line, for use with VSCode
            debug = frc.getDebugOrDefault(false)
        }
        // Built in artifact to deploy arbitrary files to the roboRIO.
        fileTreeArtifact('frcStaticFileDeploy') {
            // The directory below is the local directory to deploy
            files = fileTree(dir: 'src/main/deploy')
            // Deploy to RoboRIO target, into /home/lvuser/deploy
            targets << "roborio"
            directory = '/home/lvuser/deploy'
        }
    }
}

// Set this to true to include the src folder in the include directories passed
// to the compiler. Some eclipse project imports depend on this behavior.
// We recommend leaving this disabled if possible. Note for eclipse project
// imports this is enabled by default. For new projects, its disabled
def includeSrcInIncludeRoot = true

// Set this to true to enable desktop support.
def includeDesktopSupport = false

model {
    components {
        frcUserProgram(NativeExecutableSpec) {
            targetPlatform wpi.platforms.roborio
            if (includeDesktopSupport) {
                targetPlatform wpi.platforms.desktop
            }

            sources.cpp {
                source {
                    srcDir 'src/main/cpp'
                    include '**/*.cpp'
                }
                exportedHeaders {
                    srcDir 'src/main/include'
                    if (includeSrcInIncludeRoot) {
                        srcDir 'src/main/cpp'
                    }
                }
            }

            // Defining my dependencies. In this case, WPILib (+ friends), and vendor libraries.
            useLibrary(it, "wpilib")
            wpi.deps.vendor.cpp(it)
        }
    }
    testSuites {
        frcUserProgramTest(GoogleTestTestSuiteSpec) {
            testing $.components.frcUserProgram

            sources.cpp {
                source {
                    srcDir 'src/test/cpp'
                    include '**/*.cpp'
                }
            }

            useLibrary(it, "wpilib", "googletest")
            wpi.deps.vendor.cpp(it)
        }
    }
}

/**
 * Custom 973 Tasks
 */

// Doxygen
doxygen {
    group = 'Custom 973 Tasks'
    description = 'Generate documentation'

    project_name '2019-inseason'

    generate_html true
    generate_latex false

    source 'src/main/cpp'
    source 'guides/'
    source 'README.md'

    use_mdfile_as_mainpage 'README.md'

    outputDir 'docs/'

    executables {
        doxygen version : '1.8.15'
    }
}

// clang-format
task style(type: Exec) {
    group = 'Custom 973 Tasks'
    description = 'Automatically style your code'

    commandLine './tools/style/enforce_style.sh'
}

// clang-format commit hook
task installStyleHook(type: Exec) {
    group = 'Custom 973 Tasks'
    description = 'Install the style commit hook that gets run before commits'

    commandLine './tools/style/install_style_check_hook.sh'
}

// log clear
task clearLogs(type: Exec) {
    group = 'Custom 973 Tasks'
    description = 'Clear robot logs'

    commandLine './tools/log/clearlogs.sh', frc.getTeamNumber()
}

// log pull
task downloadLogs(type: Exec) {
    group = 'Custom 973 Tasks'
    description = 'Download robot logs'

    commandLine 'python3', './tools/log/downloadlogs.py', frc.getTeamNumber()
}
